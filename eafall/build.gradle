apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

int target_v = 21

buildscript {
    ext {
        kotlin_v = '1.0.0-beta-3593'
        exclude_java = []
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_v"
    }
}

repositories {
    jcenter()
}

dependencies {
    compile project(':andenginemultiplayerextension')
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_v"
    compile "com.android.support:appcompat-v7:21.0.2"
    compile 'com.google.android.gms:play-services-analytics:8.4.0'
    compile 'com.android.support:multidex:1.0.1'
}

android {
    compileSdkVersion target_v
    buildToolsVersion "21.1.2"

    packagingOptions {
        pickFirst 'META-INF/LICENSE.txt'
    }

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion target_v

        multiDexEnabled true

        testInstrumentationRunner "com.yaroslavlancelot.eafall.test.DefaultInstrumentationTestRunner"
        testApplicationId "com.yaroslavlancelot.eafall.test"
        applicationId "com.yaroslavlancelot.eafall"
    }

    productFlavors {
        demo {
            exclude_java.clear()
            buildConfigField("Boolean", "DEMO_VERSION", "true")
            //network
            exclude_java << "com/yaroslavlancelot/eafall/network/**"
            exclude_java << "com/yaroslavlancelot/eafall/android/activities/multiplayer/**"
            exclude_java << "com/yaroslavlancelot/eafall/game/client/thick/server/**"
            exclude_java << "com/yaroslavlancelot/eafall/game/client/thin/**"
            //campaign
            exclude_java << "com/yaroslavlancelot/eafall/game/campaign/**"
        }
        dev {
            exclude_java.clear()
            buildConfigField("Boolean", "DEMO_VERSION", "false")
        }
    }

    sourceSets {
        main {
            java {
                for (files in exclude_java) {
                    exclude files
                }
            }
        }
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        main.java.srcDirs += "src/main/kotlin"
    }

//    signingConfigs {
//        release {
//            def console = System.console()
//            if (console != null) {
//                storeFile file("keystore/eafall_demo.keystore")
//                def pass = new String(console.readPassword("\n\$ Enter keystore password: "))
//                storePassword "$pass"
//                keyPassword "$pass"
//                keyAlias console.readLine("\n\$ Enter key alias: ")
//            }
//        }
//    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
//            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    applicationVariants.all { variant ->
        //remove unneeded resources from demo build
        if (variant.name.contains('demo')) {
            def excludeAssetsFromDemo = task("excludeAssetsFrom${variant.name}", type: Delete) {
                String assetsPath = "${buildDir}/intermediates/assets/${variant.dirName}"
                delete "${assetsPath}/campaign"
                delete "${assetsPath}/graphics/sprites/alliance/imperials"
                delete "${assetsPath}/graphics/sprites/alliance/rebels"
                delete "${assetsPath}/graphics/images/alliance/imperials"
                delete "${assetsPath}/graphics/images/alliance/rebels"
            }
            variant.mergeAssets.finalizedBy excludeAssetsFromDemo
        }
    }
}

//doesn't work if it's at the top of the file
apply plugin: 'com.google.gms.google-services'